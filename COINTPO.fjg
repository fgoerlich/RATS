/*
     MULTIVARIATE TRACE STATISTIC FOR COINTEGRATION
     PHILLIPS AND OULARIS (1990)                     July/93 - R1: March/03
*/
COM [STRING]                    %%COINTPO = 'COINTPO - Version R1: March/03'

PROC  COINTPO    TBEGP TENDP
*
LOCAL INTEGER    TBEG  TEND  V I
LOCAL SERIES     TIME
LOCAL VECT[INT]  VLIST
LOCAL VECT[SER]  RLIST
LOCAL SYMM       ZZ
LOCAL RECT       Y X Z
LOCAL VECT       L
*
OPTION CHOICE  REGRESS  2 TREND CTE NONE
OPTION INTEGER N1WINDOW 0
OPTION INTEGER N2WINDOW 4
OPTION SWITCH  PRINT    0

ENTER(VARYING,ENTRIES=V) VLIST
INQUIRE(REGRESSORLIST) TBEG>>TBEGP  TEND>>TENDP
# VLIST
MAKE Y TBEG TEND
# VLIST
DIM  ZZ(V,V) RLIST(V) %BETA(V)

DIS 'MULTIVARIATE TRACE STATISTIC - PHILLIPS & OULARIS (1990)'
DIS 'SERIES:' %L(VLIST(1)) @19 'FROM' %DATELABEL(TBEG) 'TO' %DATELABEL(TEND)
DO I = 2,V
   DIS @9 %L(VLIST(I))
END DO I

IF REGRESS.EQ.1
   {
   DIS 'VAR INCLUDES A CONSTANT AND A LINEAR TREND'
   SET   TIME TBEG TEND = T - TBEG
   LABEL TIME
   #    'Trend'
   MAKE X TBEG TEND
   # CONSTANT TIME
   COM ZZ = TR(Y)*Y - (TR(Y)*X)*(INV(TR(X)*X)*(TR(X)*Y))
   SYS 1 TO V
   VAR VLIST
   DET CONSTANT TIME
   LAG 1
   END(SYS)
   }
ELSE IF REGRESS.EQ.2
   {
   DIS 'VAR INCLUDES A CONSTANT'
   MAKE X TBEG TEND
   # CONSTANT
   COM ZZ = TR(Y)*Y - (TR(Y)*X)*(INV(TR(X)*X)*(TR(X)*Y))
   SYS 1 TO V
   VAR VLIST
   DET CONSTANT
   LAG 1
   END(SYS)
   }
ELSE
   {
   DIS 'VAR DOES NOT INCLUDE DETERMINISTIC COMPONENTES'
   COM ZZ = TR(Y)*Y
   SYS 1 TO V
   VAR VLIST
   LAG 1
   END(SYS)
   }

ESTIMATE(PRINT=PRINT,NOFTEST,RES=RLIST) TBEG+1 TEND
DIS
DO I = N1WINDOW,N2WINDOW
   MCOV(LAGS=I,DAMP=1) TBEG+1 TEND
   # RLIST
   DIS @5 'WINDOW SIZE =' ## I @+3 'PZ =' ###.#### %NOBS*%TRACE(%CMOM*INV(ZZ))
END DO I
DIS
DIS 'ORTHOGONAL LEAST SQUARES ESTIMATION'
EIG  ZZ L Z
DIS 'SMALLEST EIGENVALUE:' L(V)
DIS
DIS ' LABEL    VAR    COEFFICIENT     NORMALIZED'
DIS '*******   ***   *************   ************'
DO I = 1,V
   DIS @2 %L(VLIST(I)) @12 VLIST(I) @@^8 Z(I,V) @30 Z(I,V)/Z(1,V)
   COM %BETA(I) = Z(I,V)/Z(1,V)
END DO I
DIS

CLE TIME RLIST
END PROC COINTPO
