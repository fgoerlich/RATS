/*
   LIFETABLEINE                                      April/08 - R0: April/08
*/
COM [STRING]           %%LIFETABLEINE = 'LIFETABLEINE - Version R0: April/08'

PROC   LIFETABLEINE  P D1 D2   OUTEV   OUTLT
TYPE   SERIES        P D1 D2  *OUTEV
TYPE   RECT                           *OUTLT

LOCAL  SERIES     AGE Q L D LL TT EV  C
LOCAL  STRING     HEADER   
LOCAL  INTEGER    N
LOCAL  REAL       RADIX  SLL
LOCAL  RECT       CLT
LOCAL  VECT       G

OPTION STRING     HEADERL
OPTION SWITCH     PRINT    1
OPTION SWITCH     INTEGER  1
OPTION SWITCH     LEXIS    1
OPTION INTEGER    RADIXL
OPTION RECT       V01
OPTION CHOICE     FINAL    1  85  100  110

*   Globals
COM %%AGE = %%EVAGE = %%PV = %%PVPC = %NA

INQ(REG) * N
# P D1 D2

IF .NOT.(N==101.OR.N==111)
   {
   DIS %%LIFETABLEINE; DIS 'Sintax Error: Final age should be at 100+ or 110+ years only'
   HALT  LIFETABLEINE
   }

IF %DEFINED(HEADERL)
   COM HEADER = HEADERL
ELSE
   DIS(STORE=HEADER) 'Life table from' %L(P) 'population data. INE method'
COM RADIX = %IF(%DEFINED(RADIXL),FLOAT(RADIXL),100000)

SET AGE 1 111 = T-1
IF LEXIS
   SET Q =     (D1 + D2) /(P +    D1)
ELSE
   SET Q = (.5*(D1 + D2))/(P + .5*D1)

IF LEXIS==0
   COM Q(1) = %IF(%DEFINED(V01),(V01(1,1)+V01(1,2))/(P(1)+V01(1,1)),(.7*D1(1)+.3*D2(1))/(P(1)+.7*D1(1))),  $
       Q(2) = %IF(%DEFINED(V01),(V01(2,1)+V01(2,2))/(P(2)+V01(2,1)),(.6*D1(2)+.4*D2(2))/(P(2)+.6*D1(2)))

*   INE smoothing of death probabilities at older ages
COM G = %DEATHINE(Q)
SET Q 1 111 = %IF(T<=91,Q,%IF(T==111,1,G(T-91)))

*   INE´s life table calculations from Q only.
SET(FIRST=RADIX)    L  1 111 = %IF(INTEGER==1,%ROUND(L{1}*(1-Q{1}),0),L{1}*(1-Q{1}))
SET                 D  1 111 = %IF(T==111,L,L-L{-1})
SET                 LL 1 111 = %IF(T==111,L,L+L{-1})*.5
COM SLL = %SUM(LL)
SET(FIRST=SLL)      TT 1 111 = %IF(T==111,LL,TT{1}-LL{1})
SET                 EV 1 111 = %IF(L==0,0   ,TT/L)

MAKE  CLT 1 111
# AGE Q L D LL TT EV

COM %%AGE = %MEANAGE(P), %%EVAGE = %LEATMEANAGE(%%AGE,EV), %%PV = %LIFEPOTENTIAL(P,EV), %%PVPC = %LIFEPOTENTIALPC(P,EV)

IF PRINT
   {
   SET C = D1+D2
   IF .NOT.LEXIS
      SET C = .5*C
   @LIFETABLEPRINT(HEADER=HEADER,PRINT) P C CLT
   DIS 'Basic Demographic indicators  - Years'
   DIS 'Mean Age'       @+15  ###.##  %%AGE   @+1 '- Life Expectacy' @+12       ##.## %%EVAGE
   DIS 'Potential Life' @@>12 *.      %%PV    @+4 '- Potential Life per capita' ##.## %%PVPC
   DIS;DIS
   *   Abridge Life Table
   @LIFETABLEC2A(HEADER='Life table data - Abridged',PRI,FINAL=FINAL) CLT
   DIS;DIS
   }

IF %DEFINED(OUTEV);   SET OUTEV 1 111 = EV
IF %DEFINED(OUTLT);   COM OUTLT       = CLT

CLE AGE Q L D LL TT EV C
END PROC LIFETABLEINE