*   This file contains Rats FUNCTIONs for LIFE calculations
*   They are NOT for general use.
*   WinRATS 8.10
*   Paco Goerlich   6/6/2012

********************************************************************************

*   %BIRTHINE - INE adjustment at age zero (for abridged Lifetables).

FUNCTION   %BIRTHINE  M
TYPE REAL  %BIRTHINE  M

COM  %BIRTHINE = 0.07 + 1.7*M

END  %BIRTHINE

********************************************************************************

*   %BIRTHHMD - HMD/Coale-Demeny (1983) adjustment at age zero, [0,1).

FUNCTION   %BIRTHHMD  M  S
TYPE REAL  %BIRTHHMD  M
TYPE INT                 S

LOCA REAL  A01M A01F

* S: 1 = both sexes combined / 2 = males / 3 = females
COM  A01M = %IF(M.GE.0.107,0.330,0.045 + 2.684*M),  $
     A01F = %IF(M.GE.0.107,0.350,0.053 + 2.800*M)

IF      S==1; COM  %BIRTHHMD = (A01M + A01F)/2
ELSE IF S==2; COM  %BIRTHHMD =  A01M
ELSE IF S==3; COM  %BIRTHHMD =         A01F
ELSE        ; COM  %BIRTHHMD =  %NA

END  %BIRTHHMD

********************************************************************************

*   %BIRTHHMDW - %BIRTHHMD sex weighted for total population.

FUNCTION   %BIRTHHMDW  V
TYPE REAL  %BIRTHHMDW
TYPE RECT              V

LOCA REAL  M01M M01F

COM  M01M = V(2,1)/V(1,1), M01F = V(2,2)/V(1,2)
IF V(2,1)==0.AND.V(2,2)==0
   COM %BIRTHHMDW =  %BIRTHHMD(0,1)
ELSE
   COM %BIRTHHMDW = (%BIRTHHMD(M01M,2)*V(2,1) + %BIRTHHMD(M01F,3)*V(2,2))/(V(2,1) + V(2,2))

END    %BIRTHHMDW

********************************************************************************

*   %BIRTHHMD14 - HMD/Coale-Demeny (1983) adjustment at age [1,5).

FUNCTION   %BIRTHHMD14  M  S
TYPE REAL  %BIRTHHMD14  M
TYPE INT                   S

LOCA REAL  A14M A14F

* S: 1 = both sexes combined / 2 = males / 3 = females
COM  A14M = %IF(M.GE.0.107,1.352,1.651 - 2.816*M),  $
     A14F = %IF(M.GE.0.107,1.361,1.522 - 1.518*M)

IF      S==1; COM  %BIRTHHMD14 = (A14M + A14F)/2
ELSE IF S==2; COM  %BIRTHHMD14 =  A14M
ELSE IF S==3; COM  %BIRTHHMD14 =         A14F
ELSE        ; COM  %BIRTHHMD14 =  %NA

END  %BIRTHHMD14

********************************************************************************

*   %BIRTHHMD14W - %BIRTHHMD sex weighted for total population.

FUNCTION   %BIRTHHMD14W  V
TYPE REAL  %BIRTHHMD14W
TYPE RECT                V

LOCA REAL  M01M M01F

COM  M01M = V(2,1)/V(1,1), M01F = V(2,2)/V(1,2)
IF V(2,1)==0.AND.V(2,2)==0
   COM %BIRTHHMD14W =  %BIRTHHMD14(0,1)
ELSE
   COM %BIRTHHMD14W = (%BIRTHHMD14(M01M,2)*V(2,1) + %BIRTHHMD14(M01F,3)*V(2,2))/(V(2,1) + V(2,2))

END    %BIRTHHMD14W

********************************************************************************

*   %KEYFTIZ - Keyftiz & Flieger (1990) average lenth data above age 5.

FUNCTION   %KEYFTIZ
TYPE VEC   %KEYFTIZ

COM  %KEYFTIZ = || 2.500 | 3.143 | 2.724 | 2.520 | 2.481 | 2.601 | 2.701 | 2.663 | $
                   2.698 | 2.676 | 2.645 | 2.624 | 2.619 | 2.593 | 2.518 | 2.423 ||

END  %KEYFTIZ

********************************************************************************

*   %DEATHINE - INE smoothing of death probabilities at older ages.
*               Observations of Y are assumed to run from 1 to 101 (INE data),
*                                                 or from 1 to 111 (HMD data)

FUNCTION   %DEATHINE  Y
TYPE VEC   %DEATHINE
TYPE SER              Y

LOCA SER   X  X2  X3  YF

SET X  91 111 = T-93
SET X2 91 111 = X**2
SET X3 91 111 = X**3

LIN(NOPRINT) Y  91  95
#  CONSTANT  X  X2  X3

RES(CREATE,NOPRINT) 3 
# 1  2       3        4
# 1  X(91)   X2(91)   X3(91)   Y(91)
# 1  2       3        4
# 1  X(111)  X2(111)  X3(111)  1
#    2       3        4
#    1     2*X(111) 3*X2(111)  0

PRJ  YF 91 111

DIM  %DEATHINE(19)
EWI  %DEATHINE(I) = YF(91+I)

CLE  X  X2  X3  YF
END  %DEATHINE

********************************************************************************

*   %DEATHHMD - HMD smoothing of death rates at older ages.
*               Observations of Y are assumed to run from 1 to 101 (INE data),
*                                                 or from 1 to 111 (HMD data)

FUNCTION   %DEATHHMD  Y  W  K
TYPE VEC   %DEATHHMD
TYPE SER              Y  W
TYPE INT                    K

LOCA SER  Z  X  YF
LOCA INT  J

INQ(SER=Y) *   J
SET  Z    81   J = %IF(Y.GE.1,%NA,LOG(Y/(1-Y)))
SET  X    81 111 = (T-1) + 0.5

*  K: 1 = WLS / !=1 = OLS
IF K==1
   {
   SET                YF     81 J = 1/W
   LIN(NOPRINT,SPREAD=YF) Z  81 J
   # CONSTANT X
   }
ELSE
   {
   LIN(NOPRINT)           Z  81 J
   # CONSTANT X
   }

CLE YF
PRJ YF 81 111

COM J = 81
WHILE J.LE.95
   {
   IF W(J).LT.100
      BREAK
   ELSE
      COM J = J + 1
   }

SET YF 81 111 = %IF(T.LT.J,Y,EXP(YF)/(1+EXP(YF)))

DIM %DEATHHMD(31)
EWI %DEATHHMD(I) = YF(80+I)

CLE  Z  X  YF
END  %DEATHHMD

********************************************************************************

*   %MINHMDMPv5 - Minimum age at which we start using smoothed rates (INTEGER).
*                 This age is the same for both sexes.
*                 The search is done for ages 80 to 94, since from 95 onwards
*                 we use smoothed rates anyway.

FUNCTION   %MINHMDMPv5  DF  DM
TYPE INT   %MINHMDMPv5
TYPE SER                DF  DM

LOCA INT J1 J2

COM J1 = 81
   WHILE J1<=95
      {
      IF DF(J1)<100
         BREAK
      ELSE
         COM J1 = J1 + 1
      }

COM J2 = 81
   WHILE J2<=95
      {
      IF DM(J2)<100
         BREAK
      ELSE
         COM J2 = J2 + 1
      }
COM %MINHMDMPv5 = %IMIN(J1,J2)

END %MINHMDMPv5

********************************************************************************

*   %DEATHHMDMPv5 - HMD smoothing of death rates at older ages according to MPv5.
*                   Observations of Y are assumed to run from 1 to 101 (INE data),
*                                                     or from 1 to 111 (HMD data)
*                   Calculations:
*                   (1) If only 1 positive value of M is encountered this is used 
*                       as a constant death rate for ages 80 and above.
*                   (2) Direct maximization of LogL (54) in HMDMPv5. Initial values
*                       are obtanied by NLLS of a Poisson regression model.
*                       If no converge occurs then the linear method of the HMD is
*                       used.
*                       Note: After some convergence problems (version 8.10, Male
*                             data, 1978, Guipuzcoa), the initial values are NOT
*                             taken from the NLLS Poisson regresion model, and we
*                             go directly to the MLE with the BFGS option.
*                             Initial values are A = 0.05 & B = 0.1 and Exposure
*                             is adjusted to Deaths if Deaths > Exposure: %MAX(D,E)

FUNCTION   %DEATHHMDMPv5  D  E   K
TYPE VEC   %DEATHHMDMPv5
TYPE SER                  D  E
TYPE INT                         K

LOCA SER   M  SMPL  Z  X  YF
LOCA INT   I  J
LOCA REAL  A A0 B B0
LOCA FRML  AB  MU  LOGL

INQ(REG) * J
# D E

DIM %DEATHHMDMPv5(31)
SET M 81 J = D/%MAX(0,E)

IF %POSITIVE(M)>=2
   {
   SET SMPL 81 J = %IF(%VALID(M).AND.M>0,1,0)
   NONLIN A0 B0
   FRML AB    = A=EXP(A0), B=EXP(B0), A*EXP(B*(T+0.5-81))
   FRML MU    = AB/(1 + AB)
   FRML LOGL  = D*LOG(MU) - %MAX(D,E)*MU
   COM  A0 = log(0.05), B0 = log(0.1)
   *   Final MLE estimation
   MAX(METHOD=BFGS,SMPL=SMPL,NOPRINT) LOGL 81 J

   IF %CONVERGED.AND.%DET(%XX)<>0
      EWI %DEATHHMDMPv5(I) = %IF(80+I<K,M(80+I),MU(80+I))
   ELSE
      {
      SET Z  81   J = %IF(M>=1,%NA,LOG(M/(1-M)))
      SET X  81 111 = (T-1) + 0.5
      SET YF 81   J = 1/D
      LIN(SMPL=SMPL,NOPRINT,SPREAD=YF) Z 81 J
      # CONSTANT X
      CLE YF
      PRJ YF 81 111
      EWI %DEATHHMDMPv5(I) = %IF(80+I<K,M(80+I),EXP(YF(80+I))/(1+EXP(YF(80+I))))
      }
   }
ELSE
   EWI %DEATHHMDMPv5(I) = %MAXVALUE(M)

CLE  M SMPL Z X YF
END  %DEATHHMDMPv5

********************************************************************************

*   %DEATHHMDMPv5W - HMD weights for total population death rates at older ages 
*                    according to MPv5.
*                    Observations of Y are assumed to run from 1 to 101 (INE data),
*                                                      or from 1 to 111 (HMD data)

FUNCTION   %DEATHHMDMPv5W  EF  EM  K
TYPE VEC   %DEATHHMDMPv5W
TYPE SER                   EF  EM
TYPE INT                           K

LOCA SER  E  Z  X  X2  F
LOCA INT  J

INQ(REG) *   J
# EF  EM
DIM %DEATHHMDMPv5W(31)
SET  E    81   J = EF/(EF + EM)
SET  Z    81   J = LOG(E/(1-E))
SET  X    81 111 = T-1
SET  X2   81 111 = X**2

SET                F     81 J = 1/(EF+EM)
LIN(NOPRINT,SPREAD=F) Z  81 J
# CONSTANT X X2

CLE F
PRJ F 81 111
EWI %DEATHHMDMPv5W(I) = %IF(80+I<K,E(80+I),EXP(F(80+I))/(1+EXP(F(80+I))))

CLE  E  Z  X  X2  F
END  %DEATHHMDMPv5W

********************************************************************************

*   %AGE - Age vector of labels.

FUNCTION      %AGE  K
TYPE VEC[LAB] %AGE
TYPE INT            K

IF      K==15
   COM %AGE = || '      0' | '  1-  4' | '  5-  9' | ' 10- 14' | ' 15- 19' | ' 20- 24' | ' 25- 29' | $
                 ' 30- 34' | ' 35- 39' | ' 40- 44' | ' 45- 49' | ' 50- 54' | ' 55- 59' | ' 60- 64' | $
                 '    +65' ||
ELSE IF K==16
   COM %AGE = || '      0' | '  1-  4' | '  5-  9' | ' 10- 14' | ' 15- 19' | ' 20- 24' | ' 25- 29' | $
                 ' 30- 34' | ' 35- 39' | ' 40- 44' | ' 45- 49' | ' 50- 54' | ' 55- 59' | ' 60- 64' | $
                 ' 65- 69' | '    +70' ||
ELSE IF K==17
   COM %AGE = || '      0' | '  1-  4' | '  5-  9' | ' 10- 14' | ' 15- 19' | ' 20- 24' | ' 25- 29' | $
                 ' 30- 34' | ' 35- 39' | ' 40- 44' | ' 45- 49' | ' 50- 54' | ' 55- 59' | ' 60- 64' | $
                 ' 65- 69' | ' 70- 74' | '    +75' ||
ELSE IF K==18
   COM %AGE = || '      0' | '  1-  4' | '  5-  9' | ' 10- 14' | ' 15- 19' | ' 20- 24' | ' 25- 29' | $
                 ' 30- 34' | ' 35- 39' | ' 40- 44' | ' 45- 49' | ' 50- 54' | ' 55- 59' | ' 60- 64' | $
                 ' 65- 69' | ' 70- 74' | ' 75- 79' | '    +80' ||
ELSE IF K==19
   COM %AGE = || '      0' | '  1-  4' | '  5-  9' | ' 10- 14' | ' 15- 19' | ' 20- 24' | ' 25- 29' | $
                 ' 30- 34' | ' 35- 39' | ' 40- 44' | ' 45- 49' | ' 50- 54' | ' 55- 59' | ' 60- 64' | $
                 ' 65- 69' | ' 70- 74' | ' 75- 79' | ' 80- 84' | '    +85' ||
ELSE IF K==20
   COM %AGE = || '      0' | '  1-  4' | '  5-  9' | ' 10- 14' | ' 15- 19' | ' 20- 24' | ' 25- 29' | $
                 ' 30- 34' | ' 35- 39' | ' 40- 44' | ' 45- 49' | ' 50- 54' | ' 55- 59' | ' 60- 64' | $
                 ' 65- 69' | ' 70- 74' | ' 75- 79' | ' 80- 84' | ' 85- 89' | '    +90' ||
ELSE IF K==21
   COM %AGE = || '      0' | '  1-  4' | '  5-  9' | ' 10- 14' | ' 15- 19' | ' 20- 24' | ' 25- 29' | $
                 ' 30- 34' | ' 35- 39' | ' 40- 44' | ' 45- 49' | ' 50- 54' | ' 55- 59' | ' 60- 64' | $
                 ' 65- 69' | ' 70- 74' | ' 75- 79' | ' 80- 84' | ' 85- 89' | ' 90- 94' | '    +95' ||
ELSE IF K==22
   COM %AGE = || '      0' | '  1-  4' | '  5-  9' | ' 10- 14' | ' 15- 19' | ' 20- 24' | ' 25- 29' | $
                 ' 30- 34' | ' 35- 39' | ' 40- 44' | ' 45- 49' | ' 50- 54' | ' 55- 59' | ' 60- 64' | $
                 ' 65- 69' | ' 70- 74' | ' 75- 79' | ' 80- 84' | ' 85- 89' | ' 90- 94' | ' 95- 99' | $
                 '   +100' ||
ELSE IF K==23
   COM %AGE = || '      0' | '  1-  4' | '  5-  9' | ' 10- 14' | ' 15- 19' | ' 20- 24' | ' 25- 29' | $
                 ' 30- 34' | ' 35- 39' | ' 40- 44' | ' 45- 49' | ' 50- 54' | ' 55- 59' | ' 60- 64' | $
                 ' 65- 69' | ' 70- 74' | ' 75- 79' | ' 80- 84' | ' 85- 89' | ' 90- 94' | ' 95- 99' | $
                 '100-104' | '   +105' ||
ELSE IF K==24
   COM %AGE = || '      0' | '  1-  4' | '  5-  9' | ' 10- 14' | ' 15- 19' | ' 20- 24' | ' 25- 29' | $
                 ' 30- 34' | ' 35- 39' | ' 40- 44' | ' 45- 49' | ' 50- 54' | ' 55- 59' | ' 60- 64' | $
                 ' 65- 69' | ' 70- 74' | ' 75- 79' | ' 80- 84' | ' 85- 89' | ' 90- 94' | ' 95- 99' | $
                 '100-104' | '105-109' | '   +110' ||
ELSE IF K==86
   COM %AGE = || '    0 ·' | '    1 ·' | '    2 ·' | '    3 ·' | '    4 ·' | '    5 ·' | '    6 ·' | $
                 '    7 ·' | '    8 ·' | '    9 ·' | '   10 ·' | '   11 ·' | '   12 ·' | '   13 ·' | $
                 '   14 ·' | '   15 ·' | '   16 ·' | '   17 ·' | '   18 ·' | '   19 ·' | '   20 ·' | $
                 '   21 ·' | '   22 ·' | '   23 ·' | '   24 ·' | '   25 ·' | '   26 ·' | '   27 ·' | $
                 '   28 ·' | '   29 ·' | '   30 ·' | '   31 ·' | '   32 ·' | '   33 ·' | '   34 ·' | $
                 '   35 ·' | '   36 ·' | '   37 ·' | '   38 ·' | '   39 ·' | '   40 ·' | '   41 ·' | $
                 '   42 ·' | '   43 ·' | '   44 ·' | '   45 ·' | '   46 ·' | '   47 ·' | '   48 ·' | $
                 '   49 ·' | '   50 ·' | '   51 ·' | '   52 ·' | '   53 ·' | '   54 ·' | '   55 ·' | $
                 '   56 ·' | '   57 ·' | '   58 ·' | '   59 ·' | '   60 ·' | '   61 ·' | '   62 ·' | $
                 '   63 ·' | '   64 ·' | '   65 ·' | '   66 ·' | '   67 ·' | '   68 ·' | '   69 ·' | $
                 '   70 ·' | '   71 ·' | '   72 ·' | '   73 ·' | '   74 ·' | '   75 ·' | '   76 ·' | $
                 '   77 ·' | '   78 ·' | '   79 ·' | '   80 ·' | '   81 ·' | '   82 ·' | '   83 ·' | $
                 '   84 ·' | '  +85 ·' ||
ELSE IF K==101
   COM %AGE = || '    0 ·' | '    1 ·' | '    2 ·' | '    3 ·' | '    4 ·' | '    5 ·' | '    6 ·' | $
                 '    7 ·' | '    8 ·' | '    9 ·' | '   10 ·' | '   11 ·' | '   12 ·' | '   13 ·' | $
                 '   14 ·' | '   15 ·' | '   16 ·' | '   17 ·' | '   18 ·' | '   19 ·' | '   20 ·' | $
                 '   21 ·' | '   22 ·' | '   23 ·' | '   24 ·' | '   25 ·' | '   26 ·' | '   27 ·' | $
                 '   28 ·' | '   29 ·' | '   30 ·' | '   31 ·' | '   32 ·' | '   33 ·' | '   34 ·' | $
                 '   35 ·' | '   36 ·' | '   37 ·' | '   38 ·' | '   39 ·' | '   40 ·' | '   41 ·' | $
                 '   42 ·' | '   43 ·' | '   44 ·' | '   45 ·' | '   46 ·' | '   47 ·' | '   48 ·' | $
                 '   49 ·' | '   50 ·' | '   51 ·' | '   52 ·' | '   53 ·' | '   54 ·' | '   55 ·' | $
                 '   56 ·' | '   57 ·' | '   58 ·' | '   59 ·' | '   60 ·' | '   61 ·' | '   62 ·' | $
                 '   63 ·' | '   64 ·' | '   65 ·' | '   66 ·' | '   67 ·' | '   68 ·' | '   69 ·' | $
                 '   70 ·' | '   71 ·' | '   72 ·' | '   73 ·' | '   74 ·' | '   75 ·' | '   76 ·' | $
                 '   77 ·' | '   78 ·' | '   79 ·' | '   80 ·' | '   81 ·' | '   82 ·' | '   83 ·' | $
                 '   84 ·' | '   85 ·' | '   86 ·' | '   87 ·' | '   88 ·' | '   89 ·' | '   90 ·' | $
                 '   91 ·' | '   92 ·' | '   93 ·' | '   94 ·' | '   95 ·' | '   96 ·' | '   97 ·' | $
                 '   98 ·' | '   99 ·' | ' +100 ·' ||
ELSE IF K==111
   COM %AGE = || '    0 .' | '    1 ·' | '    2 ·' | '    3 ·' | '    4 ·' | '    5 ·' | '    6 ·' | $
                 '    7 ·' | '    8 ·' | '    9 ·' | '   10 ·' | '   11 ·' | '   12 ·' | '   13 ·' | $
                 '   14 ·' | '   15 ·' | '   16 ·' | '   17 ·' | '   18 ·' | '   19 ·' | '   20 ·' | $
                 '   21 ·' | '   22 ·' | '   23 ·' | '   24 ·' | '   25 ·' | '   26 ·' | '   27 ·' | $
                 '   28 ·' | '   29 ·' | '   30 ·' | '   31 ·' | '   32 ·' | '   33 ·' | '   34 ·' | $
                 '   35 ·' | '   36 ·' | '   37 ·' | '   38 ·' | '   39 ·' | '   40 ·' | '   41 ·' | $
                 '   42 ·' | '   43 ·' | '   44 ·' | '   45 ·' | '   46 ·' | '   47 ·' | '   48 ·' | $
                 '   49 ·' | '   50 ·' | '   51 ·' | '   52 ·' | '   53 ·' | '   54 ·' | '   55 ·' | $
                 '   56 ·' | '   57 ·' | '   58 ·' | '   59 ·' | '   60 ·' | '   61 ·' | '   62 ·' | $
                 '   63 ·' | '   64 ·' | '   65 ·' | '   66 ·' | '   67 ·' | '   68 ·' | '   69 ·' | $
                 '   70 ·' | '   71 ·' | '   72 ·' | '   73 ·' | '   74 ·' | '   75 ·' | '   76 ·' | $
                 '   77 ·' | '   78 ·' | '   79 ·' | '   80 ·' | '   81 ·' | '   82 ·' | '   83 ·' | $
                 '   84 ·' | '   85 ·' | '   86 ·' | '   87 ·' | '   88 ·' | '   89 ·' | '   90 ·' | $
                 '   91 ·' | '   92 ·' | '   93 ·' | '   94 ·' | '   95 ·' | '   96 ·' | '   97 ·' | $
                 '   98 ·' | '   99 ·' | '  100 ·' | '  101 ·' | '  102 ·' | '  103 ·' | '  104 ·' | $
                 '  105 ·' | '  106 ·' | '  107 ·' | '  108 ·' | '  109 ·' | ' +110 ·' ||
ELSE
   COM %AGE = || '       ' ||

END    %AGE

********************************************************************************

*   %FINAL - Number of data points for Abridged LIFETABLES.

FUNCTION %FINAL I
TYPE INT %FINAL I

IF      I==1 ; COM %FINAL = 15
ELSE IF I==2 ; COM %FINAL = 16
ELSE IF I==3 ; COM %FINAL = 17
ELSE IF I==4 ; COM %FINAL = 18
ELSE IF I==5 ; COM %FINAL = 19
ELSE IF I==6 ; COM %FINAL = 20
ELSE IF I==7 ; COM %FINAL = 21
ELSE IF I==8 ; COM %FINAL = 22
ELSE IF I==9 ; COM %FINAL = 23
ELSE IF I==10; COM %FINAL = 24
ELSE         ; COM %FINAL = 0

END      %FINAL

********************************************************************************

*   %COM2ABR - From Complete TO Abridge LIFETABLES.

FUNCTION %COM2ABR  I  J
TYPE INT %COM2ABR  I  J

IF      I==86 ; COM %COM2ABR = %IMIN(19,%FINAL(J))
ELSE IF I==101; COM %COM2ABR = %IMIN(22,%FINAL(J))
ELSE IF I==111; COM %COM2ABR = %IMIN(24,%FINAL(J))
ELSE          ; COM %COM2ABR =        0

END      %COM2ABR

********************************************************************************

*   %ABR2AGE - Abridge Observations TO Ages in Abridge LIFETABLES.

FUNCTION %ABR2AGE  I
TYPE INT %ABR2AGE  I

IF      I==15; COM %ABR2AGE =  65
ELSE IF I==16; COM %ABR2AGE =  70
ELSE IF I==17; COM %ABR2AGE =  75
ELSE IF I==18; COM %ABR2AGE =  80
ELSE IF I==19; COM %ABR2AGE =  85
ELSE IF I==20; COM %ABR2AGE =  90
ELSE IF I==21; COM %ABR2AGE =  95
ELSE IF I==22; COM %ABR2AGE = 100
ELSE IF I==23; COM %ABR2AGE = 105
ELSE IF I==24; COM %ABR2AGE = 110
ELSE         ; COM %ABR2AGE =   0

END      %ABR2AGE

********************************************************************************

*   %MEANAGE - Mean Age from population data.

FUNCTION  %MEANAGE  POPULATION 
TYPE REA  %MEANAGE
TYPE SER            POPULATION

LOCAL INT N
LOCAL SER AGE

INQ(SER=POPULATION) * N
IF      N==19.OR.N==22.OR.N==24  ;   SET AGE 1 N = %IF(T<=2,T-1,(T-2)*5)
ELSE IF N==86.OR.N==101.OR.N==111;   SET AGE 1 N =          T-1
ELSE                             ;   SET AGE 1 N = %NA
SET(SCRATCH) AGE 1 N = %IF(T==N,AGE + 112, AGE + AGE{-1})/2
COM %MEANAGE = %M(AGE,POPULATION)

CLE  AGE
END %MEANAGE

********************************************************************************

*   %LEATMEANAGE - Life Expectancy at Mean Age.
*                  From Life Expectacy data and a given age.
*                  Results are restricted for ages [5,85).

FUNCTION  %LEATMEANAGE  AGE  LIFEXP
TYPE REA  %LEATMEANAGE  AGE
TYPE SER                     LIFEXP

LOCAL INT N  I
LOCAL REA A0 A1

INQ(SER=LIFEXP) * N
IF      N==19.OR.N==22.OR.N==24  ;   COM A0 = FIX(AGE/5)*5, A1 = A0 + 5, I = FIX(AGE/5) + 2
ELSE IF N==86.OR.N==101.OR.N==111;   COM A0 = FIX(AGE)    , A1 = A0 + 1, I = FIX(AGE)   + 1
ELSE                             ;   COM A0 = A1 = %NA                 , I = 0

IF AGE<5.OR.AGE>=85
   COM  %LEATMEANAGE = %NA
ELSE
   COM  %LEATMEANAGE = %GPLININT(A0,LIFEXP(I),A1,LIFEXP(I+1),AGE)

END     %LEATMEANAGE

********************************************************************************

*   %LIFEPOTENTIAL - Life Potential from Life Expectancy and Population data.

FUNCTION  %LIFEPOTENTIAL  POPULATION  LIFEXP
TYPE REA  %LIFEPOTENTIAL
TYPE SER                  POPULATION  LIFEXP

LOCAL INT N
LOCAL SER E

INQ(REG) * N
# POPULATION LIFEXP

SET E 1 N = %IF(T==N,LIFEXP,LIFEXP + LIFEXP{-1})/2
COM %LIFEPOTENTIAL = %DOT(E,POPULATION)

CLE  E
END %LIFEPOTENTIAL

********************************************************************************

*   %LIFEPOTENTIALPC - Life Potential per capita from Life Expectancy and 
*                      Population data.

FUNCTION  %LIFEPOTENTIALPC  POPULATION  LIFEXP
TYPE REA  %LIFEPOTENTIALPC
TYPE SER                    POPULATION  LIFEXP

COM %LIFEPOTENTIALPC = %LIFEPOTENTIAL(POPULATION,LIFEXP)/%SUM(POPULATION)

END %LIFEPOTENTIALPC

********************************************************************************

*   List of functions

*   %BIRTHINE        - INE adjustment at age zero (for abridged Lifetables).
*   %BIRTHHMD        - HMD/Coale-Demeny (1983) adjustment at age zero, [0,1).
*   %BIRTHHMDW       - %BIRTHHMD sex weighted for total population.
*   %BIRTHHMD14      - HMD/Coale-Demeny (1983) adjustment at age [1,5).
*   %BIRTHHMD14W     - %BIRTHHMD sex weighted for total population.
*   %KEYFTIZ         - Keyftiz & Flieger (1990) average lenth data above age 5.
*   %DEATHINE        - INE smoothing of death probabilities at older ages.
*                      Observations of Y are assumed to run from 1 to 101 (INE data),
*                                                        or from 1 to 111 (HMD data)
*   %DEATHHMD        - HMD smoothing of death rates at older ages.
*                      Observations of Y are assumed to run from 1 to 101 (INE data),
*                                                        or from 1 to 111 (HMD data)
*   %MINHMDMPv5      - Minimum age at which we start using smoothed rates (INTEGER).
*                      This age is the same for both sexes.
*                      The search is done for ages 80 to 94, since from 95 onwards
*                      we use smoothed rates anyway.
*   %DEATHHMDMPv5    - HMD smoothing of death rates at older ages according to MPv5.
*                      Observations of Y are assumed to run from 1 to 101 (INE data),
*                                                        or from 1 to 111 (HMD data)
*   %DEATHHMDMPv5W   - HMD weights for total population death rates at older ages 
*                      according to MPv5.
*                      Observations of Y are assumed to run from 1 to 101 (INE data),
*                                                        or from 1 to 111 (HMD data)
*   %AGE             - Age vector of lables.
*   %FINAL           - Number of data points for Abridged LIFETABLES.
*   %COM2ABR         - From Complete TO Abridge LIFETABLES.
*   %ABR2AGE         - Abridge Observations TO Ages in Abridge LIFETABLES.
*   %MEANAGE         - Mean Age from ages and population data.
*   %LEATMEANAGE     - Life Expectancy at Mean Age.
*                      From Life Expectacy data and a given age.
*   %LIFEPOTENTIAL   - Life Potential from Life Expectancy and Population data.
*   %LIFEPOTENTIALPC - Life Potential per capita from Life Expectancy and 
*                      Population data.

********************************************************************************
