/*
     RAS                                      December/97 - R5: February/13
*/
COM [STRING]                       %%RAS   = 'RAS - Version R5: February/13'

PROC RAS  A  B  C  E
TYPE RECT A       *E
TYPE VECT    B  C

LOCAL  RECT     D
LOCAL  INT      I

OPTION REAL    CVCRIT        1E-15
OPTION INTEGER ITERATIONS  100
OPTION INTEGER IITERATIONS 100
OPTION SWITCH  PRINT         0
OPTION SWITCH  INTEGER       0

IF %ROWS(B)<>%ROWS(A).OR.%ROWS(C)<>%COLS(A)
   {
   DIS %%RAS; DIS 'Sintax error: Matrix and vectors to adjust are incompatible'
   HALT  RAS
   }
IF %MINVALUE(A)<0.OR.%MINVALUE(B)<0.OR.%MINVALUE(C)<0
   {
   DIS %%RAS; DIS 'Sintax error: Negative values are not allowed'
   HALT  RAS
   }
IF ABS(%SUM(B)-%SUM(C))>CVCRIT
   {
   DIS %%RAS; DIS 'Sintax error: Sum of vectors to adjust is NOT the same'
   HALT  RAS
   }

COM D =  %RAS(A,B,C,CVCRIT,ITERATIONS), I = %ITERS

*   Iteration limit
IF %CONVERGED==0
   {
   DIS %%RAS; DIS 'Convergence NOT achieved after' %ITERS 'iterations'
   IF INTEGER
      DIS 'INTEGER adjustment is not attempted'
   RETURN
   }

IF %CONVERGED.AND.(.NOT.INTEGER)
   {
   DIS %%RAS; DIS 'Convergence achieved at iteration' %ITERS
   IF PRINT
      DIS 'Adjusted matrix: ' %IF(%DEFINED(E),%LABEL(E),'') D
   }

IF INTEGER
   {
   COM D = %IRAS(D,B,C,CVCRIT,ITERATIONS,IITERATIONS)
   IF %CONVERGED==0.OR.%%CONVERGED==0
      COM D = %IRAS2(A,B,C,CVCRIT,ITERATIONS,IITERATIONS)

   *   Iteration limit
   IF %CONVERGED==0
      {
      DIS %%RAS; DIS 'Real convergence NOT achieved after' %ITERS 'real and' %%ITERS 'integer iterations'
      RETURN
      }
   ELSE IF %%CONVERGED==0
      {
      DIS %%RAS; DIS 'Integer convergence NOT achieved after' %%ITERS 'integer iterations'
      RETURN
      } 
   ELSE
      {
      DIS %%RAS; DIS 'Convergence achieved at integer iteration' %%ITERS 'after initial' I 'real iterations'
      COM %ITERS = I
      IF PRINT
         DIS 'Adjusted matrix: ' %IF(%DEFINED(E),%LABEL(E),'') D
      }
   }

IF %DEFINED(E);   COM E = D

END PROC RAS